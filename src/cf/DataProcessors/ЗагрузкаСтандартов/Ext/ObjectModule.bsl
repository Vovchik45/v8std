#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция АдресСтандартовПоУмолчанию() Экспорт
	Возврат "https://its.1c.ru/db/v8std";
КонецФункции

Функция ФункционалСППРДоступен() Экспорт
	
	Если Метаданные.Справочники.Найти("СтандартыРазработки") = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат НЕ Метаданные.Справочники["СтандартыРазработки"].Реквизиты.Найти("СсылкаНаСтандарт") = Неопределено;
	
КонецФункции

Процедура АнализРекурсивно(ДеревоРодитель, НомерПоПорядку = 0) Экспорт 
	
	ДанныеАдреса = ДанныеРесурса(ДеревоРодитель.Адрес);
	ДеревоРодитель.КодСтандарта = ДанныеАдреса.КодСтандарта;
	
	Если ДанныеАдреса.Ошибка Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(ДеревоРодитель.Адрес, "hdoc") Тогда
		ДеревоРодитель.КонечныйАдрес = ДанныеАдреса.КонечныйАдрес;
		Возврат;
	КонецЕсли;
	
	ДеревоРодитель.КонечныйАдрес = ДанныеАдреса.Адрес;
	
	ВложенныеСсылки = ДанныеАдреса.ВложенныеСсылки;
	Если ВложенныеСсылки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ДанныеСсылки Из ВложенныеСсылки Цикл
		
		НомерПоПорядку = НомерПоПорядку + 1;
		
		НовСтрока = ДеревоРодитель.Строки.Добавить();
		НовСтрока.Адрес = ДанныеСсылки.Адрес;
		НовСтрока.Заголовок = ДанныеСсылки.Заголовок;
		НовСтрока.НомерПоПорядку = НомерПоПорядку;
		
		АнализРекурсивно(НовСтрока, НомерПоПорядку);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура АктуализироватьСтандартыРекурсивно(ДеревоРодитель, ТолькоСоздаватьНовые = Ложь) Экспорт
	
	Для Каждого СтрокаДерева Из ДеревоРодитель.Строки Цикл
		
		ЭтоГруппа = НЕ СтрокаДерева.Строки.Количество() = 0;
		
		Если НЕ ЭтоГруппа Тогда
			СоздатьОбновитьСтандарт(СтрокаДерева, ТолькоСоздаватьНовые);
			Продолжить;
		КонецЕсли;
		
		СоздатьОбновитьГруппуСтандартов(СтрокаДерева, ТолькоСоздаватьНовые);
		
		АктуализироватьСтандартыРекурсивно(СтрокаДерева);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеРесурса(АдресРесурса, Таймаут = 10, Повторно = Ложь)
	
	ДанныеАдреса = Новый Структура;
	ДанныеАдреса.Вставить("Адрес", АдресРесурса);
	ДанныеАдреса.Вставить("КодСостояния", Неопределено);
	ДанныеАдреса.Вставить("Заголовок", "");
	ДанныеАдреса.Вставить("Ошибка", Ложь);
	ДанныеАдреса.Вставить("ТекстОшибки", "");
	ДанныеАдреса.Вставить("КонечныйАдрес", "");
	ДанныеАдреса.Вставить("АдресСервера", "");
	ДанныеАдреса.Вставить("ВложенныеСсылки", Новый Массив);
	ДанныеАдреса.Вставить("КодСтандарта", "");
	
	СтруктураURI = СтруктураURI(АдресРесурса);
	
	ДанныеАдреса.АдресСервера = СтруктураURI.Схема + "://" + СтруктураURI.ИмяСервера;

	HTTPСоединение = Новый HTTPСоединение(СтруктураURI.Хост, СтруктураURI.Порт, , , , Таймаут);
	HTTPЗапрос = Новый HTTPЗапрос(СтруктураURI.ПутьНаСервере); 
	
	Попытка
		Результат = HTTPСоединение.Получить(HTTPЗапрос);
	Исключение
		
		// Если не достучались попробуем повторно и увеличим таймаут
		Если Не Повторно Тогда
			Возврат ДанныеРесурса(АдресРесурса, Таймаут * 2, Истина);
		КонецЕсли;
		
		ТекстОшибки = ОписаниеОшибки();
		
		ДанныеАдреса.Ошибка = Истина;
		ДанныеАдреса.ТекстОшибки = ТекстОшибки;
		
		ЗаписьЖурналаРегистрации("Ошибка получения стандарта с ИТС",
									УровеньЖурналаРегистрации.Ошибка, ,
									АдресРесурса, 
									ТекстОшибки);
		Возврат ДанныеАдреса;
	КонецПопытки;
	
	ДанныеАдреса.КодСостояния = Результат.КодСостояния;
	
	// Обрабатываем отсутствие страницы
	Если Результат.КодСостояния = 404 Тогда
		
		ДанныеАдреса.Ошибка = Истина;
		ДанныеАдреса.ТекстОшибки = "Страница не найдена";
		
		Возврат ДанныеАдреса;
	КонецЕсли;
	
	// Обрабатываем перенаправление
	Если Результат.КодСостояния = 302 Тогда
		АдресРесурса = Результат.Заголовки.Получить("Location");
		Если АдресРесурса = Неопределено Тогда
			ДанныеАдреса.Ошибка = Истина;
			ДанныеАдреса.ТекстОшибки = "Перенаправление: сервер не сообщил адрес ресурса!";
		КонецЕсли;
		
		Возврат ДанныеРесурса(АдресРесурса);
	КонецЕсли;
	
	ТелоОтвета = Результат.ПолучитьТелоКакСтроку();
	
	ДанныеАдреса.Заголовок = ПоискЗаголовка(ТелоОтвета);
	ДанныеАдреса.КонечныйАдрес = ДанныеАдреса.АдресСервера + "/" + ПоискКонечногоАдреса(ТелоОтвета);
	ДанныеАдреса.ВложенныеСсылки = ПоискВложенныхСсылок(ТелоОтвета, ДанныеАдреса.АдресСервера);
	ДанныеАдреса.КодСтандарта = ПоискКодаСтандарта(АдресРесурса);
	
	Возврат ДанныеАдреса;
	
КонецФункции

Функция СтруктураURI(Знач СтрокаURI) Экспорт
	
	СтрокаURI = СокрЛП(СтрокаURI);
	
	// схема
	Схема = "";
	Позиция = СтрНайти(СтрокаURI, "://");
	Если Позиция > 0 Тогда
		Схема = НРег(Лев(СтрокаURI, Позиция - 1));
		СтрокаURI = Сред(СтрокаURI, Позиция + 3);
	КонецЕсли;
	
	// строка соединения и путь на сервере
	СтрокаСоединения = СтрокаURI;
	ПутьНаСервере = "";
	Позиция = СтрНайти(СтрокаСоединения, "/");
	Если Позиция > 0 Тогда
		ПутьНаСервере = Сред(СтрокаСоединения, Позиция + 1);
		СтрокаСоединения = Лев(СтрокаСоединения, Позиция - 1);
	КонецЕсли;
	
	// информация пользователя и имя сервера
	СтрокаАвторизации = "";
	ИмяСервера = СтрокаСоединения;
	Позиция = СтрНайти(СтрокаСоединения, "@");
	Если Позиция > 0 Тогда
		СтрокаАвторизации = Лев(СтрокаСоединения, Позиция - 1);
		ИмяСервера = Сред(СтрокаСоединения, Позиция + 1);
	КонецЕсли;
	
	// логин и пароль
	Логин = СтрокаАвторизации;
	Пароль = "";
	Позиция = СтрНайти(СтрокаАвторизации, ":");
	Если Позиция > 0 Тогда
		Логин = Лев(СтрокаАвторизации, Позиция - 1);
		Пароль = Сред(СтрокаАвторизации, Позиция + 1);
	КонецЕсли;
	
	// хост и порт
	Хост = ИмяСервера;
	Порт = "";
	Позиция = СтрНайти(ИмяСервера, ":");
	Если Позиция > 0 Тогда
		Хост = Лев(ИмяСервера, Позиция - 1);
		Порт = Сред(ИмяСервера, Позиция + 1);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Схема", Схема);
	Результат.Вставить("Логин", Логин);
	Результат.Вставить("Пароль", Пароль);
	Результат.Вставить("ИмяСервера", ИмяСервера);
	Результат.Вставить("Хост", Хост);
	Результат.Вставить("Порт", ?(Порт <> "", Число(Порт), Неопределено));
	Результат.Вставить("ПутьНаСервере", ПутьНаСервере);
	
	Возврат Результат;
	
КонецФункции

Функция ПоискВложенныхСсылок(Знач Текст, Знач АдресСервера = "")
	
	Ссылки = Новый Массив;
	
	// Обрезаем всё до "nav list", чтобы было меньше мусора
	Текст = Сред(Текст, СтрНайти(Текст, "class=""nav list""") - 5, СтрДлина(Текст));
	
	// Ищем все href в блоке <a ... /a>"
	РегулярноеВыражение = "<a\s+href=""([^""]*)"">([^<]*)<\/a>";
	
	МассивРезультат = СтрНайтиВсеПоРегулярномуВыражению(Текст, РегулярноеВыражение, Истина);
	Для Каждого Результат Из МассивРезультат Цикл
		
		СтрокаЗначение = Результат.Значение;
		
		Начало = СтрНайти(СтрокаЗначение, "href=");
		Если Начало = 0 Тогда
			Продолжить;
		КонецЕсли;
		Начало = Начало + 6;
		Конец = СтрНайти(СтрокаЗначение, ">") - 1;
		
		Адрес = Сред(СтрокаЗначение, Начало, Конец - Начало);
		
		СтрокаЗначение = Сред(СтрокаЗначение, Конец, СтрДлина(СтрокаЗначение));
		
		Начало = СтрНайти(СтрокаЗначение, ">");
		Если Начало = 0 Тогда
			Продолжить;
		КонецЕсли;
		Начало = Начало + 1;
		Конец = СтрНайти(СтрокаЗначение, "<");
		Заголовок = Сред(СтрокаЗначение, Начало, Конец - Начало);
		Заголовок = СтрЗаменить(Заголовок, "&quot;", "");
		
		Ссылки.Добавить(Новый Структура("Заголовок, Адрес", Заголовок, АдресСервера + Адрес));
		
	КонецЦикла;
	
	Возврат Ссылки;
	
КонецФункции

Функция ПоискЗаголовка(Текст)
	
	// Ищем title в блоке <h1 ... >"
	РегулярноеВыражение = "<h1[^>]*title=""([^""]*)""[^>]*>";
	
	Значение = СтрНайтиПоРегулярномуВыражению(Текст, РегулярноеВыражение, , , , Истина).Значение;
	Значение = СтрЗаменить(Значение, "&quot;", "");
	Значение = Сред(Значение, СтрНайти(Значение, "title") + 7, СтрДлина(Значение));
	Значение = Лев(Значение, СтрДлина(Значение) - 2);
	
	Возврат Значение;
	
КонецФункции

Функция ПоискКодаСтандарта(Текст)
	
	// Ищем всё что находится между content и hdoc 
	РегулярноеВыражение = "content/(\d\s*)+/hdoc";
	
	Значение = СтрНайтиПоРегулярномуВыражению(Текст, РегулярноеВыражение, , , , Истина).Значение;
	Значение = СтрЗаменить(Значение, "&quot;", "");
	Значение = СтрЗаменить(Значение, "content/", "");
	Значение = СтрЗаменить(Значение, "/hdoc", "");
	Значение = СтрЗаменить(Значение, " ", "");
	
	Возврат Значение;
	
КонецФункции

Функция ПоискКонечногоАдреса(Текст)
	
	// Ищем href в блоке <base ... >"
	РегулярноеВыражение = "<base[^>]*id=[^>]*href=""([^""]*)""";
	
	Значение = СтрНайтиПоРегулярномуВыражению(Текст, РегулярноеВыражение, , , , Истина).Значение;
	Значение = СтрЗаменить(Значение, "&quot;", "");
	Значение = Сред(Значение, СтрНайти(Значение, "href=") + 7, СтрДлина(Значение));
	Значение = Лев(Значение, СтрДлина(Значение) - 1);
	
	Возврат Значение;
	
КонецФункции

Процедура ЗаменаСсылокНаКартинкиBase64(АдресСтраницы, ТекстХТМЛ)
	
	ПоследнийСлеш = СтрНайти(АдресСтраницы, "/", НаправлениеПоиска.СКонца);
	БазовыйАдресКартинок = Лев(АдресСтраницы, ПоследнийСлеш);
	ТегиСКартинками = АдресаКартинок(ТекстХТМЛ);
	
	Для Каждого РезультатПоиска Из ТегиСКартинками Цикл
		ИсходныйТег = РезультатПоиска.Значение;
		
		НачалоСсылкиНаКартинку = СтрНайти(ИсходныйТег, "src=""") + 5;
		КонецСсылкиНаКартинку = СтрНайти(ИсходныйТег, """", , НачалоСсылкиНаКартинку);
		ОтносительнаяСсылкаНаКартинку = Сред(ИсходныйТег, НачалоСсылкиНаКартинку, КонецСсылкиНаКартинку - НачалоСсылкиНаКартинку);
		
		АбсолютнаяСсылкаНаКартинку = БазовыйАдресКартинок + ОтносительнаяСсылкаНаКартинку;
		ДанныеКартинки = КоннекторHTTP.КакДвоичныеДанные(КоннекторHTTP.Get(АбсолютнаяСсылкаНаКартинку));
		КартинкаBase64 = Base64Строка(ДанныеКартинки);
		
		ИсходнаяСсылкаНаКартинку = СтрШаблон("src=""%1""", ОтносительнаяСсылкаНаКартинку);
		НоваяСсылкаНаКартинку = СтрШаблон("src=""data:image/png;base64, %1""", КартинкаBase64);
		
		НовыйТег = СтрЗаменить(ИсходныйТег, ИсходнаяСсылкаНаКартинку, НоваяСсылкаНаКартинку);
		
		ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, ИсходныйТег, НовыйТег);
	КонецЦикла;
	
КонецПроцедуры

Функция АдресаКартинок(ТекстХТМЛ)
	
	// Ищем src в блоках <img ... >"
	РегулярноеВыражение = "<img\s[^>]*?src\s*=\s*['\""]([^'\""]*?)['\""][^>]*?>";
	Возврат СтрНайтиВсеПоРегулярномуВыражению(ТекстХТМЛ, РегулярноеВыражение, Истина);
	
КонецФункции

#Область АктуализацияСправочникаСтандарты

Процедура СоздатьОбновитьСтандарт(СтрокаДерева, ТолькоСоздаватьНовые = Ложь)
	
	ДлинаКода = 9;
	ШаблонКода = "ИТС000000" + СтрокаДерева.КодСтандарта;
	КодСтандарта = Лев(ШаблонКода, 3) + Прав(ШаблонКода, 6);
	
	СтандартСсылка = Справочники.СтандартыРазработки.НайтиПоКоду(КодСтандарта);
	
	Если Не ЗначениеЗаполнено(СтандартСсылка) Тогда
		СтандартСсылка = СтандартПоНаименованию(СтрокаДерева.Заголовок);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтандартСсылка) Тогда
		СтрокаДерева.Стандарт = СтандартСсылка;
		
		Если СтрокаДерева.СтандартОбработан ИЛИ ТолькоСоздаватьНовые Тогда
			Возврат;
		КонецЕсли;
		
		Попытка
			СтандартОбъект = СтандартСсылка.ПолучитьОбъект();
		Исключение 
			ЗаписьЖурналаРегистрации("Ошибка получения объекта",
										УровеньЖурналаРегистрации.Ошибка,
										Метаданные.Справочники.СтандартыРазработки,
										СтандартСсылка,
										ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
		
	Иначе
		СтандартОбъект = Справочники.СтандартыРазработки.СоздатьЭлемент();
		СтандартОбъект.Код = КодСтандарта;
		СтандартОбъект.Внешний = Истина;
		СтандартОбъект.ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СтандартОбъект.ДатаИзменения = ТекущаяДатаСеанса();
	СтандартОбъект.Наименование = СтрокаДерева.Заголовок;
	СтандартОбъект.НомерПоПорядку = СтрокаДерева.НомерПоПорядку;
	
	ОписаниеЧисла = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0, ДопустимыйЗнак.Неотрицательный));
	СтандартОбъект.КодСтандарта = ОписаниеЧисла.ПривестиЗначение(СтрокаДерева.КодСтандарта);
	СтандартОбъект.АдресСодержания = СтрокаДерева.КонечныйАдрес;
	
	СодержаниеСтандартаХТМЛ = СодержаниеСтандартаХТМЛ(СтандартОбъект.АдресСодержания);
	ЗаменаСсылокНаКартинкиBase64(СтандартОбъект.АдресСодержания, СодержаниеСтандартаХТМЛ);
	
	СтандартОбъект.УстановитьСодержаниеСтандарта(СодержаниеСтандартаХТМЛ);
	
	Если Не СтрокаДерева.Родитель = Неопределено Тогда
		СтандартОбъект.Родитель = СтрокаДерева.Родитель.Стандарт;
	КонецЕсли;
	
	СтандартОбъект.СсылкаНаСтандарт = СтрокаДерева.Адрес;
	
	Попытка
		СтандартОбъект.Записать();
	Исключение
		ЗаписьЖурналаРегистрации("Ошибка записи объекта",
										УровеньЖурналаРегистрации.Ошибка,
										Метаданные.Справочники.СтандартыРазработки,
										СтандартСсылка,
										ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	СтрокаДерева.СтандартОбработан = Истина;
	СтрокаДерева.Стандарт = СтандартОбъект.Ссылка;
	
КонецПроцедуры

Процедура СоздатьОбновитьГруппуСтандартов(СтрокаДерева, ТолькоСоздаватьНовые = Ложь)
	
	СтандартСсылка = СтандартПоНаименованию(СтрокаДерева.Заголовок, Истина);
	
	СтандартОбъект = Неопределено;
	
	Если ЗначениеЗаполнено(СтандартСсылка) Тогда
		СтрокаДерева.Стандарт = СтандартСсылка;
		
		Если СтрокаДерева.СтандартОбработан ИЛИ ТолькоСоздаватьНовые Тогда
			Возврат;
		КонецЕсли;
		
		Попытка
			СтандартОбъект = СтандартСсылка.ПолучитьОбъект();
		Исключение
			ЗаписьЖурналаРегистрации("Ошибка получения объекта",
										УровеньЖурналаРегистрации.Ошибка,
										Метаданные.Справочники.СтандартыРазработки,
										СтандартСсылка,
										ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат;
		КонецПопытки;
	Иначе
		СтандартОбъект = Справочники.СтандартыРазработки.СоздатьГруппу();
	КонецЕсли;
	
	СтандартОбъект.Наименование = СтрокаДерева.Заголовок;
	СтандартОбъект.НомерПоПорядку = СтрокаДерева.НомерПоПорядку;
	
	Если Не СтрокаДерева.Родитель = Неопределено Тогда
		СтандартОбъект.Родитель = СтрокаДерева.Родитель.Стандарт;
	КонецЕсли;
	
	СтандартОбъект.СсылкаНаСтандарт = СтрокаДерева.Адрес;
	
	Попытка
		СтандартОбъект.Записать();
	Исключение
		ЗаписьЖурналаРегистрации("Ошибка записи объекта",
										УровеньЖурналаРегистрации.Ошибка,
										Метаданные.Справочники.СтандартыРазработки,
										СтандартСсылка,
										ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	СтрокаДерева.СтандартОбработан = Истина;
	СтрокаДерева.Стандарт = СтандартОбъект.Ссылка;
	
КонецПроцедуры

Функция СтандартПоНаименованию(Заголовок, ЭтоГруппа = Ложь)
	
	Перем Выборка, Запрос;
	
	// Тех. долг. Запрос в цикле.
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ Первые 1
	|	СтандартыРазработки.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтандартыРазработки КАК СтандартыРазработки
	|ГДЕ
	|	СтандартыРазработки.ЭтоГруппа = &ЭтоГруппа
	|	И СтандартыРазработки.Наименование = &Наименование";
	
	Запрос.УстановитьПараметр("ЭтоГруппа", ЭтоГруппа);
	Запрос.УстановитьПараметр("Наименование", Заголовок);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Справочники.СтандартыРазработки.ПустаяСсылка();

КонецФункции

Функция СодержаниеСтандартаХТМЛ(СсылкаНаСодержание)
	
	СодержаниеХТМЛ = КоннекторHTTP.КакТекст(КоннекторHTTP.Get(СсылкаНаСодержание), "windows-1251");
	СодержаниеХТМЛ = СтрЗаменить(СодержаниеХТМЛ, Символы.ПС, "");
	// замена кодировки для уменьшения проблем в будущем
	СодержаниеХТМЛ = СтрЗаменить(СодержаниеХТМЛ, "charset=windows-1251", "charset=utf-8");
	
	Возврат СодержаниеХТМЛ;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли
