#Область ОбработчикиСобытий

Процедура ПриВыбореРезультатаГлобальногоПоиска(ЭлементРезультата, СтандартнаяОбработка)
	
	Если ТипЗнч(ЭлементРезультата.Значение) <> Тип("СправочникСсылка.СтандартыРазработки") Тогда
		Возврат;
	КонецЕсли;
	
	// Форма работы со стандартами отображается на начальной странице
	НачальнаяСтраница = РаботаСОкнамиКлиент.НачальнаяСтраница();
	
	// На начальной странице может быть несколько форм
	ФормаСоСтандартами = РаботаСОкнамиКлиент.ФормаПросмотраСтандартов(НачальнаяСтраница);
	
	Если ФормаСоСтандартами <> Неопределено Тогда
		
		СтандартнаяОбработка = Ложь;
		// Иногда переход к строке в списке может не происходить, баг платформы,
		// хотя при этом содержание считывается и выводится на форму.
		// чтобы при пееключении на элемент, расположенный выше по иерархии (не считая элементов, не групп, в корне),
		// происходила визуализация переключения. 1С не считает это ошибкой.
		ФормаСоСтандартами.Элементы.Список.ТекущаяСтрока = ПредопределенноеЗначение("Справочник.СтандартыРазработки.ПустаяСсылка");
		ФормаСоСтандартами.Элементы.Список.ТекущаяСтрока = ЭлементРезультата.Значение;
		// Начальная страница может быть не активна
		НачальнаяСтраница.Активизировать();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриГлобальномПоиске(СтрокаПоиска, ПланПоиска)
	
	// Форма работы со стандартами отображается на начальной странице
	НачальнаяСтраница = РаботаСОкнамиКлиент.НачальнаяСтраница();
	
	// На начальной странице может быть несколько форм
	ФормаСоСтандартами = РаботаСОкнамиКлиент.ФормаПросмотраСтандартов(НачальнаяСтраница);
	
	Если ФормаСоСтандартами <> Неопределено Тогда
		// Чтобы отработала подсветка строки поиска в тексте стандарта
		ФормаСоСтандартами.СтрокаГлобальногоПоиска = СтрокаПоиска;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
